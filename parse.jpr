(mac defp (name args 'body)
  `(def ,name ,args
     (fn (xs i)
       ,@body)))

(mac defletp (name args varname val 'body)
  `(def ,name ,args
     (let ,varname ,val
       (fn (xs i)
	 ,@body))))

(assign eof (fn (xs i)
	      (lif (is xs.length i)
		   (list true i)
		   '((no-eof)))))

(defletp one-of (ys)
  err `((one-of ,ys))
  (let x (get xs i)
    (if (some x ys)
	(list x (+ i 1))
	err)))

(defletp none-of (ys)
  err `((none-of ,ys))
  (let x (get xs i)
    (if (! (some x ys))
	(list x (+ i 1))
	err)))

(defp por ('fs)
  (with (errs ()
	 j 0
	 res false)
    (while (and (< j fs.length) (no res))
      (let foo ((get fs j) xs i)
	(lif (get foo 1)
	     (= res foo)
	     (do
	       (errs.push (get foo 0))
	       (++ j)))))
    (lor res `((or ,@errs)))))

(defp chain ('fs)
  (let res ()
    ((afn (fs)
       (lif fs
	    (let foo ((get fs 0) xs i)
	      (if (is foo.length 2)
		 (do
		   (res.push (get foo 0))
		   (= i (get foo 1))
		   (self (slice fs 1)))
		 `((at ,i ,@foo))))
	    (list res i)))
     fs)))

(defletp pstr (s)
  err `((,s))
  (if (is (xs.substr i s.length) s)
      (list s (+ i s.length))
      err))

(defp many (f)
  (with (res ()
	 foo undefined)
    (while (do
	     (= foo (f xs i))
	     (is foo.length 2))
      (res.push (get foo 0))
      (= i (get foo 1)))
    (list res i)))

(defp transform (f p)
  (let foo (p xs i)
    (if (is foo.length 2)
	(list (f (get foo 0)) (get foo 1))
	foo)))

(mac transp (arg body p)
  `(transform (fn (,arg)
		,body)
	      ,p))

(def many1 (f)
  (transp (x xs)
	  `(,x ,@xs)
	  (chain f (many f))))

(assign pnothing (fn (xs i)
		   (list null i)))

(def optional (f)
  (por f pnothing))

(def sep-by1 (fa fb)
  (transp (x xs)
	  `(,x ,@(map second xs))
	  (chain fa (many (chain fb fa)))))

(def sep-by (fa fb)
  (por (sep-by1 fa fb)
       (transp () () pnothing)))

(defletp prange (a b)
   err `((range ,a ,b))
  (let x (get xs i)
    (if (<= a x b)
	(list x (+ i 1))
	err)))

(defp dbgp (f)
  (let foo (f xs i)
    (prn xs i foo)
    foo))

(assign alpha (por (prange 'a 'z) (prange 'A 'Z)))

(assign digit (prange '"0" '"9"))

(assign symbol-char (por alpha (one-of '"!/%-|$<>_+*=")))

(assign symbol (por (transp (x xs) (apply str x xs)
			    (chain symbol-char (many (por symbol-char digit))))
		    (one-of '.)))

(def between (a b c)
  (transp (_ x _) x (chain a c b)))

(def start-sep-end (fa fb)
  (between fb fb (sep-by fa fb)))

(defp pthunk (f)
  ((f) xs i))

(assign escaped (transp (_ x) x
			(chain (one-of '"\\")
			       (por (transform (fn () '"\n") (one-of 'n))
				    (transform (fn () '"\t") (one-of 't))
				    (one-of '"\\\"")))))

(assign string (transp xs (apply str xs)
		       (between (one-of '"\"")
				(one-of '"\"")
				(many (por escaped (none-of '"\\\""))))))

(assign number (transform (fn ((sign a b))
			    (zap lor sign '+)
			    (zap lor b '"")
			    (asnumber (str sign (apply str a) b)))
			  (chain (optional (one-of '+-))
				 (many1 digit)
				 (optional (transp (dot digits)
						   (apply str dot digits)
						   (chain (one-of '.) (many digit)))))))

(def modifer (s)
  (por (chain (pstr s) (pthunk (fn () expression2)))
       (pstr s)))

(assign expression (apply por number symbol string (pthunk (fn () plist))
			  (pthunk (fn () bracket-list))
			  (map [modifer _] '(' ` ,@ ,))))

(assign expression2 (transp xs (if (is xs.length 1)
				   (get xs 0)
				   `(. ,@xs))
			    (sep-by1 expression (one-of '.))))

(assign sep (many (one-of '"\n\t ")))

(assign expressions (start-sep-end expression2 sep))

(assign plist (between (one-of '"(") (one-of  '")") expressions))

(assign bracket-list (transp xs `("[" ,@xs)
			     (between (one-of '"[") (one-of '"]") expressions)))

(assign pfile (transp (file _) file (chain expressions eof)))